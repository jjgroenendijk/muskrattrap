#include "TheThingsNetwork_HANIoT.h"
#include "HAN_IoT_Shield.h"
#include "secrets.h"
#include "encoder.h"
#include "decoder.h"
#include "IOTShieldConfig.h"
#include "knightRider.h"
#include "test.h"
#include "doorSensor.h"
#include "catchSensor.h"
#include "displacementSensor.h"
#include "batterySensor.h"

const char *devEui = DEVEUI;  ///< devEUI to be generated by TTN Console
const char *appEui = JOINEUI; ///< appEUI retrieve from TTN Console application
const char *appKey = APPKEY;  ///< appKEY rtrieve from TTN Console application

#define loraSerial Serial1
#define debugSerial Serial
bool loraCommunication = false; ///< Set to true if you want to use LoRa communication, false for testing without LoRa

// Replace REPLACE_ME with TTN_FP_EU868 or TTN_FP_US915
#define freqPlan TTN_FP_EU868

TheThingsNetwork_HANIoT ttn(loraSerial, debugSerial, freqPlan);

// iotShieldTempSensor temperatureSensor;

/// Declare the button objects
doorSensor doorSensor;
catchSensor catchSensor;
displacementSensor displacementSensor;
batterySensor batterySensor;
iotShieldPotmeter potmeter2_test(potmeter2);

void setup()
{
    delay(4000); // Allow bootloader to finish before using serial ports

    if (loraCommunication)
    {
        loraSerial.begin(57600);
    }

    debugSerial.begin(9600);

    // Wait a maximum of 10s for Serial Monitor
    while (!debugSerial && millis() < 10000)
        ;

    if (loraCommunication)
    {
        debugSerial.println(F("-- STATUS"));
        ttn.showStatus();

        debugSerial.println(F("-- JOIN"));
        ttn.join(devEui, appEui, appKey);
    }
}

void loop()
{
    ///< Loop indication for debugging
    debugSerial.println("-- LOOP");
    // knightRider();

    ///< Toggle door and catch status on button press using debounced method
    static bool doorClosed = false;
    static bool catchDetected = false;

    if (redButton.wasPressedDebounced()) {
        doorClosed = !doorClosed;
    }
    if (blackButton.wasPressedDebounced()) {
        catchDetected = !catchDetected;
    }

    debugSerial.print(F("-- DOOR SENSOR: "));
    debugSerial.println(doorClosed ? "CLOSED" : "OPEN");
    doorSensor.setDoorStatus(doorClosed);

    debugSerial.print(F("-- CATCH SENSOR: "));
    debugSerial.println(catchDetected ? "DETECTED" : "NONE");
    catchSensor.setCatchStatus(catchDetected);

    ///< Check the red and black button (displacement sensor)
    bool displacement = redButton.isPressed() && blackButton.isPressed();
    debugSerial.print(F("-- DISPLACEMENT SENSOR: "));
    debugSerial.println(displacement ? "DISPLACED" : "STABLE");
    displacementSensor.setDisplacementStatus(displacement);

    ///< Check the battery level
    int potRaw = analogRead(A1); // Directly read potmeter2 (A1)
    uint8_t batteryLevelPct = map(potRaw, 0, 1023, 0, 100);
    batterySensor.setBatteryLevel(batteryLevelPct);
    debugSerial.print(F("-- BATTERY LEVEL (Potmeter2): "));
    debugSerial.print(potRaw);
    debugSerial.print(F(" -> %: "));
    debugSerial.println(batteryLevelPct);

    // Always print payload info, even if loraCommunication is false
    debugSerial.println("=========================");
    debugSerial.println("LoRaWAN Payload Debug");
    debugSerial.println("-------------------------");
    payloadEncoder encoder;
    uint32_t id = 12345; // Example ID
    uint8_t version = 1;
    bool door = doorSensor.getDoorStatus();
    bool catch_ = catchSensor.getCatchStatus();
    bool disp = displacementSensor.getDisplacementStatus();
    uint8_t batt = batterySensor.getBatteryLevel();
    uint32_t unixTime = 1717891200; // Example: 2024-06-09 00:00:00 UTC
    encoder.set_id(id);
    encoder.set_version(version);
    encoder.set_doorStatus(door);
    encoder.set_catchDetect(catch_);
    encoder.set_trapDisplacement(disp);
    encoder.set_batteryStatus(batt);
    encoder.set_unixTime(unixTime);
    encoder.composePayload();
    uint8_t *payloadBuffer = encoder.getPayload();
    uint8_t payloadSize = encoder.getPayloadSize();
    debugSerial.print("Payload (");
    debugSerial.print(payloadSize);
    debugSerial.print(" bytes): ");
    for (uint8_t i = 0; i < payloadSize; ++i) {
        debugSerial.print(payloadBuffer[i], HEX);
        debugSerial.print(" ");
    }
    debugSerial.println();
    debugSerial.print("  id: ");
    debugSerial.println(id);
    debugSerial.print("  version: ");
    debugSerial.println(version);
    debugSerial.print("  doorStatus: ");
    debugSerial.println(door ? "CLOSED (1)" : "OPEN (0)");
    debugSerial.print("  catchDetect: ");
    debugSerial.println(catch_ ? "DETECTED (1)" : "NONE (0)");
    debugSerial.print("  trapDisplacement: ");
    debugSerial.println(disp ? "DISPLACED (1)" : "STABLE (0)");
    debugSerial.print("  batteryStatus: ");
    debugSerial.println(batt);
    debugSerial.print("  unixTime: ");
    debugSerial.println(unixTime);
    debugSerial.println("=========================");
    if (loraCommunication)
    {
        // Send it off
        ttn.sendBytes(payloadBuffer, payloadSize);
        delay(10000);
    }
    delay(500);
}